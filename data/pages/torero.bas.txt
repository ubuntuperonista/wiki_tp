<code basic>
10 PRINT TAB(34);"BULL"
20 PRINT TAB(15);"CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
30 DEF FNA(K)=INT(RND(1)*2+1)
200 PRINT:PRINT:PRINT
202 L=1
205 PRINT "QUIERE INSTRUCCIONES";
206 INPUT Z$
207 IF Z$="NO" THEN 400
210 PRINT "HOLA, AMANTES DE LA TAUROMAQUIA Y AFICIONADOS."
220 PRINT "AQUI ESTA SU CHANCE DE MATAR AL TORO."
230 PRINT
240 PRINT "EN CADA PASADA DEL TORO, PUEDE INTENTAR"
250 PRINT "0 - VERONICA (MOVIDA PELIGROSA DE LA CAPA)"
260 PRINT "1 - MOVIDA MENOS PELIGROSA DE LA CAPA"
270 PRINT "2 - GIRO ORDINARIO DE LA CAPA."
280 PRINT
290 PRINT "EN VEZ DE LO ANTERIO, PUEDE INTENTAR MATAR AL TORO"
300 PRINT "EN CUALQUIER TURNO CON: 4 (SOBRE LOS CUERNOS), 5 (AL CUERPO)."
310 PRINT "PERO SI FUESE USTED,"
320 PRINT "NO LO INTENTARIA ANTES DEL SEPTIMO PASE."
330 PRINT
340 PRINT "LA MUCHEDUMBRE DETERMINA QUE PREMIO MERECE"
350 PRINT "(POSTUMAMENTE SI ES NECESARIO)."
360 PRINT "CUANTO MAS VALIENTE, MEJOR PREMIO RECIBIRA."
370 PRINT
380 PRINT "AL MEJOR TRABAJO DE PICADORES Y TOREROS,"
390 PRINT "MEJORARAN SUS CHANCES."
400 PRINT
410 PRINT
420 D(5)=1
430 D(4)=1
450 DIM L$(5)
455 A=INT(RND(1)*5+1)
460 FOR I=1 TO 5
463 READ L$(I)
467 NEXT I
470 DATA "SOBERBIO","BUENO","REGULAR","MEDIOCRE","ESPANTOSO"
490 PRINT "USTED A SACADO UN ";L$(A);" TORO."
500 IF A>4 THEN 530
510 IF A<2 THEN 550
520 GOTO 570
530 PRINT "TIENE SUERTE."
540 GOTO 570
550 PRINT "SUERTE Y AL TORO. LA NECESITARA."
560 PRINT
570 PRINT
590 A$="PICADO"
595 B$="RES"
600 GOSUB 1610
610 D(1)=C
630 A$="TOREADO"
635 B$="ORES"
640 GOSUB 1610
650 D(2)=C
660 PRINT
670 PRINT
680 IF Z=1 THEN 1310
690 D(3)=D(3)+1
700 PRINT "NUMERO DE PASADA";D(3)
710 IF D(3)<3 THEN 760
720 PRINT "AQUI VIENE EL TURO. INTENTE MATARLO";
730 GOSUB 1930
735 IF Z1=1 THEN 1130
740 PRINT "MOVIMIENTO DE CAPA";
750 GOTO 800
760 PRINT "EL TORO VA A LA CARGA! USTED ES EL MATADOR--"
770 PRINT "QUIERE MATAR AL TORO";
780 GOSUB 1930
785 IF Z1=1 THEN 1130
790 PRINT "QUE MOVIMIENTO QUIERE HACER CON LA CAPA";
800 INPUT E
810 IF E<>INT(ABS(E)) THEN 830
820 IF E<3 THEN 850
830 PRINT "NO TENGA MIEDO, IDIOTA!  DE UN ESPECTACULO ADECUADO"
840 GOTO 800
850 REM
860 IF E=0 THEN 920
870 IF E=1 THEN 900
880 M=.5
890 GOTO 930
900 M=2
910 GOTO 930
920 M=3
930 L=L+M
940 F=(6-A+M/10)*RND(1)/((D(1)+D(2)+D(3)/10)*5)
950 IF F<.51 THEN 660
960 PRINT "EL TORO LO HIZO CUERO!"
970 ON FNA(0) GOTO 980,1010
980 PRINT "ESTA MUERTO."
990 D(4)=1.5
1000 GOTO 1310
1010 PRINT "USTED AUN ESTA VIVO.":PRINT
1020 PRINT "DESEA SALIR CORRIENDO DE LA ARENA";
1030 GOSUB 1930
1035 IF Z1=2 THEN 1070
1040 PRINT "COBARDE"
1050 D(4)=0
1060 GOTO 1310
1070 PRINT "USTED ES VALIENTE.  ESTUPIDO, PERO VALIENTE."
1080 ON FNA(0) GOTO 1090,1110
1090 D(4)=2
1100 GOTO 660
1110 PRINT "FUE CORNEADO NUEVAMENTE!"
1120 GOTO 970
1130 REM
1140 Z=1
1150 PRINT:PRINT "ESTE ES EL MOMENTO DE LA VERDAD.":PRINT
1155 PRINT "COMO INTENTARA MATAR AL TORO";
1160 INPUT H
1170 IF H=4 THEN 1230
1180 IF H=5 THEN 1230
1190 PRINT "TUVO MIEDO.  EL TORO LO HA CORNEADO."
1220 GOTO 970
1230 K=(6-A)*10*RND(1)/((D(1)+D(2))*5*D(3))
1240 IF J=4 THEN 1290
1250 IF K>.2 THEN 960
1260 PRINT "MATO AL TORO!"
1270 D(5)=2
1280 GOTO 1320
1290 IF K>.8 THEN 960
1300 GOTO 1260
1310 PRINT
1320 PRINT
1330 PRINT
1340 IF D(4)<>0 THEN 1390
1350 PRINT "LA MUCHEDUMBRE ABUCHEA POR DIEZ MINUTORS.  SI VUELVE A APARECER"
1360 PRINT "EN LA ARENA, LE JURAN LA MUERTE--"
1370 PRINT "A NO SER QUE EL TORO LO MATE PRIMERO."
1380 GOTO 1580
1390 DEF FNC(Q)=FND(Q)*RND(1)
1395 DEF FND(Q)=(4.5+L/6-(D(1)+D(2))*2.5+4*D(4)+2*D(5)-D(3)^2/120-A)
1400 IF D(4)<>2 THEN 1430
1410 PRINT "LA MUCHEDUMBRE APLAUDE ENLOQUECIDA!"
1420 GOTO 1450
1430 IF D(5)<>2 THEN 1450
1440 PRINT "LA MUCHEDUMBRE APLAUDE!":PRINT
1450 PRINT "LA MUCHEDUMBRE LE TIRA FLORES"
1460 IF FNC(Q)<2.4 THEN 1570
1470 IF FNC(Q)<4.9 THEN 1550
1480 IF FNC(Q)<7.4 THEN 1520
1500 PRINT "OLE!  USTED ES 'MUY HOMBRE'!! OLE!  OLE!"
1510 GOTO 1580
1520 PRINT "LAS DOS OREJAS DEL TORO!"
1530 PRINT "OLE!"
1540 GOTO 1580
1550 PRINT "UNA OREJA DEL TORO."
1560 GOTO 1580
1570 PRINT "NADA EN LO ABSOLUTO."
1580 PRINT
1590 PRINT "ADIOS":PRINT:PRINT:PRINT
1600 GOTO 2030
1610 B=3/A*RND(1)
1620 IF B<.37 THEN 1740
1630 IF B<.5 THEN 1720
1640 IF B<.63 THEN 1700
1650 IF B<.87 THEN 1680
1660 C=.1
1670 GOTO 1750
1680 C=.2
1690 GOTO 1750
1700 C=.3
1710 GOTO 1750
1720 C=.4
1730 GOTO 1750
1740 C=.5
1750 T=INT(10*C+.2)
1760 PRINT "THE ";A$;B$;" DID A ";L$(T);" JOB."
1770 IF 4>T THEN 1900
1780 IF 5=T THEN 1870
1790 ON FNA(K) GOTO 1830,1850
1800 IF A$="TOREADO" THEN 1820
1810 PRINT "UNO DE LOS CABALLOS DE LA ";A$;B$;" FUE MUERTO."
1820 ON FNA(K) GOTO 1830,1850
1830 PRINT "UNO DE LOS ";A$;B$;" FUE MUERTO."
1840 GOTO 1900
1850 PRINT "NO ";A$;B$;" HUBO MUERTO."
1860 GOTO 1900
1870 IF A$="TOREADO" THEN 1890
1880 PRINT FNA(K);"DE LOS CABALLOS DE LA ";A$;B$;" MURIO."
1890 PRINT FNA(K);"DE LA ";A$;B$;" MURIO."
1900 PRINT
1910 RETURN
1920 REM
1930 INPUT A$
1940 IF A$="SI" THEN 1990
1950 IF A$="NO" THEN 2010
1970 PRINT "RESPUESTA INCORRECTA - - POR FAVOR ESCRIBA 'SI' O 'NO'."
1980 GOTO 1930
1990 Z1=1
2000 GOTO 2020
2010 Z1=2
2020 RETURN
2030 END
</code>