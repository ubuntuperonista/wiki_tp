<code freebasic>
10 PRINT TAB(32);"AHORCADO"
20 PRINT TAB(15);"POR FAVOR USA MAYUSCULAS"
25 PRINT:PRINT:PRINT
30 DIM P$(12,12),L$(20),D$(20),N$(26),U(50)
40 C=1: N=50
50 FOR I=1 TO 20: D$(I)="-": NEXT I: M=0
60 FOR I=1 TO 26: N$(I)="": NEXT I
70 FOR I=1 TO 12: FOR J=1 TO 12: P$(I,J)=" ": NEXT J: NEXT I
80 FOR I=1 TO 12: P$(I,1)="X": NEXT I
90 FOR I=1 TO 7: P$(1,I)="X": NEXT: P$(2,7)="X"
95 IF C<N THEN 100
97 PRINT "HICISTE TODAS LAS PALABRAS!!": STOP
100 Q=INT(N*RND(1))+1
110 IF U(Q)=1 THEN 100
115 U(Q)=1: C=C+1: RESTORE: T1=0
150 FOR I=1 TO Q: READ A$: NEXT I
160 L=LEN(A$): FOR I=1 TO LEN(A$): L$(I)=MID$(A$,I,1): NEXT I
170 PRINT "HE AQUI LAS LETRAS QUE HAS USADO:"
180 FOR I=1 TO 26: PRINT N$(I);: IF N$(I+1)="" THEN 200
190 PRINT ",";: NEXT I
200 PRINT: PRINT: FOR I=1 TO L: PRINT D$(I);: NEXT I: PRINT: PRINT
210 INPUT "QUE LETRA QUIERES ARRIESGAR?";G$: R=0
220 FOR I=1 TO 26: IF N$(I)="" THEN 250
230 IF G$=N$(I) THEN PRINT "YA ARRIESGASTE DICHA LETRA ANTES!": GOTO 170
240 NEXT I: PRINT "ERROR DE PROGRAMA.  CORRELO NUEVAMENTE.": STOP
250 N$(I)=G$: T1=T1+1
260 FOR I=1 TO L: IF L$(I)=G$ THEN 280
270 NEXT I: IF R=0 THEN 290
275 GOTO 300
280 D$(I)=G$: R=R+1: GOTO 270
290 M=M+1: GOTO 400
300 FOR I=1 TO L: IF D$(I)="-" THEN 320
310 NEXT I: GOTO 390
320 PRINT: FOR I=1 TO L: PRINT D$(I);: NEXT I: PRINT: PRINT
330 INPUT "INGRESA LA PALABRA SI DESEAS ARRIESGAR";B$
340 IF A$=B$ THEN 360
350 PRINT "EQUIVOCADO.  INTENTA OTRA LETRA.": PRINT: GOTO 170
360 PRINT "CORRECTO!!  TE LLEVO";T1;"INTENTOS!"
370 INPUT "QUIERES OTRA PALABRA";W$: IF W$="SI" THEN 50
380 PRINT: PRINT "HA SIDO DIVERTIDO!  ADIOS POR AHORA.": GOTO 999
390 PRINT "DESCUBRISTE LA PALABRA!": GOTO 370
400 PRINT: PRINT: PRINT"LO SIENTO, LA PALABRA NO TIENE ESA LETRA."
410 ON M GOTO 415,420,425,430,435,440,445,450,455,460
415 PRINT "PRIMERO, DIBUJAMOS UNA CABEZA": GOTO 470
420 PRINT "AHORA DIBUJAMOS UN CUERPO.": GOTO 470
425 PRINT "A CONTINUACION DIBUJAMOS UN BRAZO.": GOTO 470
430 PRINT "EN ESTA OPORTUNIDAD ES EL OTRO BRAZO.": GOTO 470
435 PRINT "AHORA, DIBUJAMOS LA PIERNA IZQUIERDA.": GOTO 470
440 PRINT "ESTA VEZ DIBUJAMOS LA PIERNA DERECHA.": GOTO 470
445 PRINT "AHORA PONEMOS UNA MANO.": GOTO 470
450 PRINT "A CONTINUACIÃ“N LA OTRA MANO. YO NO TENGO MANOS!": GOTO 470
455 PRINT "aHORA DIBUJAMOS UNO DE LOS PIES": GOTO 470
460 PRINT "AHORA EL OTRO PIE -- ESTAS AHORCADO!!"
470 ON M GOTO 480,490,500,510,520,530,540,550,560,570
480 P$(3,6)="-": P$(3,7)="-": P$(3,8)="-": P$(4,5)="(": P$(4,6)="."
481 P$(4,8)=".":P$(4,9)=")":P$(5,6)="-":P$(5,7)="-":P$(5,8)="-":GOTO 580
490 FOR I=6 TO 9: P$(I,7)="X": NEXT I: GOTO 580
500 FOR I=4 TO 7: P$(I,I-1)="\": NEXT I: GOTO 580
510 P$(4,11)="/": P$(5,10)="/": P$(6,9)="/": P$(7,8)="/": GOTO 580
520 P$(10,6)="/": P$(11,5)="/": GOTO 580
530 P$(10,8)="\": P$(11,9)="\": GOTO 580
540 P$(3,11)="\": GOTO 580
550 P$(3,3)="/": GOTO 580
560 P$(12,10)="\": P$(12,11)="-": GOTO 580
570 P$(12,3)="-": P$(12,4)="/"
580 FOR I=1 TO 12: FOR J=1 TO 12: PRINT P$(I,J);: NEXT J
590 PRINT: NEXT I: PRINT: PRINT: IF M<>10 THEN 170
600 PRINT "LO SIENTO, HAS PERDIDO.  LA PALABRA ERA ";A$
610 PRINT "fALLASTE ESA.  DESEAS ";: GOTO 370
620 INPUT "TIPEA SI OR NO";Y$: IF LEFT$(Y$,1)="Y" THEN 50
700 DATA "VIA","","PJ","TREN","SOY","","CGT"
710 DATA "NOXA","MASA","VIDA","PESO","VOTO","CASA","RASTROJERO"
720 DATA "SUELDO","ACTUAR","PERON","MANDAR","BRADEN","AVION","NUCLEAR"
730 DATA "EVITA","JIRONES","GORILA","FEMENINO","MEDALLA","PELOTA"
740 DATA "PENDULAR","FREJULI","DESCAMISADO","REVOLUCION","QUINQUENAL","CONDUCIR"
750 DATA "AGUINALDO","VACACIONES","PRESIDENTE","SOCIALISMO","NACIONAL"
760 DATA "NACIONAL","PRIVILEGIADO","EDUCACION","TRABAJO","SINDICATO"
770 DATA "MONTONEROS","ORGANIZACION","CONDUCCION","CONSTITUCION"
780 DATA "MOVILIZACION","MAGNETOFONO","MOTONETA","JUSTICIALISTA","REIVINDICACIONES"
990 PRINT "VIVA PERON!"
999 END
</code>